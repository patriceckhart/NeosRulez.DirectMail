NeosRulez.DirectMail.RecipientController.edit = NeosRulez.DirectMail:Integration.RecipientController.Edit

prototype(NeosRulez.DirectMail:Integration.RecipientController.Edit) < prototype(Neos.Fusion:Component) {

    renderer = afx`
        <NeosRulez.DirectMail:Integration.Component.DefaultLayout title={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.edit')} >
            <div class="neos-content neos-container-fluid">
                <Neos.Fusion.Form:Form form.target.action="update" form.target.controller="Recipient" form.target.package="NeosRulez.DirectMail" form.data.recipient={recipient} form.data.selectedRecipientList={selectedRecipientList} >

                    <Neos.Fusion.Form:Neos.BackendModule.FieldContainer field.name="recipient[gender]" label="Geschlecht" >
                        <Neos.Fusion.Form:Select>
                            <Neos.Fusion.Form:Select.Option option.value="1" >{I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.input.option.male')}</Neos.Fusion.Form:Select.Option>
                            <Neos.Fusion.Form:Select.Option option.value="2" >{I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.input.option.female')}</Neos.Fusion.Form:Select.Option>
                            <Neos.Fusion.Form:Select.Option option.value="3" >{I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.input.option.divers')}</Neos.Fusion.Form:Select.Option>
                        </Neos.Fusion.Form:Select>
                    </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    <Neos.Fusion.Form:Neos.BackendModule.FieldContainer label={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.customSalutation')} field.name="recipient[customsalutation]">
                        <Neos.Fusion.Form:Input attributes.placeholder={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.customSalutation')} />
                    </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    <Neos.Fusion.Form:Neos.BackendModule.FieldContainer label={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.firstname')} field.name="recipient[firstname]">
                        <Neos.Fusion.Form:Input attributes.placeholder={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.firstname')} />
                    </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    <Neos.Fusion.Form:Neos.BackendModule.FieldContainer label={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.lastname')} field.name="recipient[lastname]">
                        <Neos.Fusion.Form:Input attributes.placeholder={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.lastname')} />
                    </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    <Neos.Fusion.Form:Neos.BackendModule.FieldContainer label={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.email')} field.name="recipient[email]">
                        <Neos.Fusion.Form:Input attributes.placeholder={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.email')} />
                    </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    <Neos.Fusion:Loop @if.render={contentDimensions} items={contentDimensions} itemName="contentDimension" itemKey="contentDimensionKey" >

                        <Neos.Fusion.Form:Neos.BackendModule.FieldContainer @if.render={contentDimensions} field.name={'dimensions[' + contentDimensionKey + ']'} label={contentDimension.label} >
                            <Neos.Fusion.Form:Select>
                                <Neos.Fusion:Loop items={contentDimension.presets} itemName="contentDimensionPreset" itemKey="contentDimensionPresetKey" >
                                    <Neos.Fusion.Form:Select.Option option.value={contentDimensionPresetKey} attributes.selected={contentDimensionPreset.selected ? true : false} >{contentDimensionPreset.label} [{contentDimensionPresetKey}]</Neos.Fusion.Form:Select.Option>
                                </Neos.Fusion:Loop>
                            </Neos.Fusion.Form:Select>
                        </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    </Neos.Fusion:Loop>

                    <Neos.Fusion:Fragment @if.render={customFields} >

                        <Neos.Fusion:Loop items={customFields} itemName="customField" itemKey="customFieldKey" >

                            <Neos.Fusion.Form:Neos.BackendModule.FieldContainer label={customField.type != 'boolean' ? customField.label : false} field.name={'customFields[' + customFieldKey + ']'} >

                                <Neos.Fusion.Form:Input @if.render={customField.type == 'string'} attributes.placeholder={customField.label} attributes.value={customFieldsValues[customFieldKey]} />

                                <div @if.render={customField.type == 'boolean'} style="display:block">
                                    <Neos.Fusion.Form:Checkbox field.value={true} attributes.checked={customFieldsValues[customFieldKey]} >
                                        {customField.label}
                                    </Neos.Fusion.Form:Checkbox>
                                </div>

                                <Neos.Fusion.Form:Select @if.render={customField.type == 'array'} >
                                    <Neos.Fusion.Form:Select.Option attributes.selected="selected" attributes.disabled="disabled" >{I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.input.option.choose')}</Neos.Fusion.Form:Select.Option>
                                    <Neos.Fusion:Loop items={customField.values} itemName="customFieldItem" itemKey="customFieldItemKey" >
                                        <Neos.Fusion.Form:Select.Option option.value={customFieldItemKey} attributes.selected={customFieldsValues[customFieldKey] == customFieldItemKey} >{customFieldItem.label}</Neos.Fusion.Form:Select.Option>
                                    </Neos.Fusion:Loop>
                                </Neos.Fusion.Form:Select>

                            </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                        </Neos.Fusion:Loop>

                    </Neos.Fusion:Fragment>

                    <Neos.Fusion.Form:Neos.BackendModule.FieldContainer field.name="recipient[recipientlist]" label={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.recipientlist')} field.multiple>
                        <Neos.Fusion:Loop items={recipientLists} itemName="recipientList" >
                            <div style="display:block">
                                <Neos.Fusion.Form:Checkbox field.value={recipientList}>
                                    {recipientList.name}
                                </Neos.Fusion.Form:Checkbox>
                            </div>
                        </Neos.Fusion:Loop>
                    </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    <div class="neos-footer">
                        <Neos.Fusion.Form:Hidden @if.render={selectedRecipientList} field.name="selectedRecipientList[identifier]" field.value={selectedRecipientList} />
                        <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary" >
                            {I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.save')}
                        </Neos.Fusion.Form:Button>
                    </div>

                </Neos.Fusion.Form:Form>
            </div>
        </NeosRulez.DirectMail:Integration.Component.DefaultLayout>
    `
}

NeosRulez.DirectMail.RecipientListController.edit = NeosRulez.DirectMail:Integration.RecipientListController.Edit

prototype(NeosRulez.DirectMail:Integration.RecipientListController.Edit) < prototype(Neos.Fusion:Component) {
    inlineStyles = ${'<style>.neos .neos-table { margin-top:80px; }</style>'}

    renderer = afx`
        <NeosRulez.DirectMail:Integration.Component.DefaultLayout title="Edit recipient list" >
            <div class="neos-content neos-container-fluid">
                <Neos.Fusion.Form:Form form.target.action="update" form.target.controller="RecipientList" form.target.package="NeosRulez.DirectMail" form.data.recipientList={recipientList} >

                    <Neos.Fusion.Form:Neos.BackendModule.FieldContainer label={I18n.translate('NeosRulez.DirectMail:Integration.Controller.RecipientList:content.name')} field.name="recipientList[name]">
                        <Neos.Fusion.Form:Input attributes.placeholder={I18n.translate('NeosRulez.DirectMail:Integration.Controller.RecipientList:content.name')} />
                    </Neos.Fusion.Form:Neos.BackendModule.FieldContainer>

                    <div style="font-size: 11px; color: grey;">ID: {recipientList.identifier}</div>

                    <div class="neos-footer">
                        <Neos.Fusion.Form:Button attributes.class="neos-button neos-button-primary" >
                            {I18n.translate('NeosRulez.DirectMail:Integration.Controller.RecipientList:content.save')}
                        </Neos.Fusion.Form:Button>
                        <Neos.Fusion:Tag tagName="a" @children="attributes.href" attributes.class="neos-button neos-button-primary" content={'<i class="fas fa-file-csv" style="margin-right:5px;"></i>' + I18n.translate('NeosRulez.DirectMail:Integration.Controller.RecipientList:content.importRecipients')} >
                            <Neos.Fusion:UriBuilder package="NeosRulez.DirectMail" controller="Import" action="index" @children="arguments">
                                <Neos.Fusion:DataStructure recipientList={recipientList} />
                            </Neos.Fusion:UriBuilder>
                        </Neos.Fusion:Tag>
                        <Neos.Fusion:Tag @if.render={!props.noCreation} tagName="a" @children="attributes.href" attributes.class="neos-button neos-button-primary" content={I18n.translate('NeosRulez.DirectMail:Integration.Controller.Recipient:content.newRecipient')} >
                            <Neos.Fusion:UriBuilder package="NeosRulez.DirectMail" controller="Recipient" action="new" @children="arguments">
                                <Neos.Fusion:DataStructure @if.render={recipientList} selectedRecipientList={recipientList} />
                            </Neos.Fusion:UriBuilder>
                        </Neos.Fusion:Tag>
                    </div>
                </Neos.Fusion.Form:Form>
                <div style="margin-top:80px;">
                    <NeosRulez.DirectMail:Integration.RecipientController.Index recipientListRecipients={recipients} action="edit" />
                </div>
            </div>
        </NeosRulez.DirectMail:Integration.Component.DefaultLayout>
    `
}
